plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.6.20'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.6.20'
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id 'application'
}

group = 'me.maxime'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url 'https://maven.pkg.jetbrains.space/public/p/kotlinx-html/maven' }
}



kotlin {
    jvm {
        compilations.all {
            kotlinOptions.jvmTarget = '11'
        }
        testRuns["test"].executionTask.configure {
            useJUnit()
        }
        withJava()
    }
    js(IR) {
        binaries.executable()
        browser {
            commonWebpackConfig {
                cssSupport.enabled = true
            }
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-datetime:0.3.2"
                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0'
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.2"
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test')
            }
        }
        jvmMain {
            dependencies {
                /*implementation 'io.ktor:ktor-server-netty:1.6.3'
                implementation 'io.ktor:ktor-html-builder:1.6.3'
                implementation 'org.jetbrains.kotlinx:kotlinx-html-jvm:0.7.2'*/

                implementation 'com.pi4j:pi4j-core:2.1.1'
                implementation 'com.pi4j:pi4j-plugin-pigpio:2.1.1'
                implementation 'com.pi4j:pi4j-plugin-raspberrypi:2.1.1'
                implementation 'com.pi4j:pi4j-library-pigpio:2.1.1'
                //implementation 'com.pi4j:pi4j-device:1.3'
                //implementation 'com.pi4j:pi4j-plugin-pigpio:1.4'

                implementation "io.ktor:ktor-server-core:1.6.8"
                implementation "io.ktor:ktor-server-netty:1.6.8"
                implementation "io.ktor:ktor-websockets:1.6.8"
                implementation "io.ktor:ktor-html-builder:1.6.8"

                implementation "org.jetbrains.kotlin:kotlin-reflect:1.6.20"
            }
        }
        jvmTest {

        }
        jsMain {
            dependencies {
            }
        }
        jsTest {

        }
    }
}

application {
    mainClassName = 'StoveServerKt'
}

tasks.named('jvmProcessResources') {
    def jsBrowserDistribution = tasks.named('jsBrowserDistribution')
    from(jsBrowserDistribution)
}

tasks.named('run') {
    dependsOn(tasks.named('jvmJar'))
    classpath(tasks.named('jvmJar'))
}

shadowJar {
    manifest {
        attributes 'Main-Class': 'StoveServerKt'
    }
}

task deployOnPi {
    dependsOn(shadowJar)
    doLast {
        exec {
            commandLine('scp', "build/libs/pistove-${version}-all.jar", System.properties['pistove.raspberry.pi.scp'])
        }
    }
}
