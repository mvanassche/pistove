plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.7.21'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.7.21'
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id 'application'
}

group = 'me.maxime'
version = '1.3-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://maven.pkg.jetbrains.space/public/p/kotlinx-html/maven' }
}



kotlin {
    jvm {
        compilations.all {
            kotlinOptions.jvmTarget = '11'
        }
        testRuns["test"].executionTask.configure {
            useJUnit()
        }
        withJava()
    }
    js(IR) {
        binaries.executable()
        browser {
            commonWebpackConfig {
                cssSupport.enabled = true
            }
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-datetime:0.4.0"
                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.1"
                implementation "io.github.microutils:kotlin-logging:3.0.4"
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test')
            }
        }
        jvmMain {
            dependencies {
                /*implementation 'io.ktor:ktor-server-netty:1.6.3'
                implementation 'io.ktor:ktor-html-builder:1.6.3'
                implementation 'org.jetbrains.kotlinx:kotlinx-html-jvm:0.7.2'*/

                implementation 'com.pi4j:pi4j-core:2.2.1'
                implementation 'com.pi4j:pi4j-plugin-pigpio:2.2.1'
                implementation 'com.pi4j:pi4j-plugin-raspberrypi:2.2.1'
                implementation 'com.pi4j:pi4j-library-pigpio:2.2.1'
                //implementation 'com.pi4j:pi4j-device:1.3'
                //implementation 'com.pi4j:pi4j-plugin-pigpio:1.4'

                implementation "io.ktor:ktor-server-core:2.1.3"
                implementation "io.ktor:ktor-server-netty:2.1.3"
                implementation "io.ktor:ktor-server-websockets:2.1.3"
                implementation "io.ktor:ktor-server-cors:2.1.3"
                implementation "io.ktor:ktor-server-html-builder:2.1.3"

                implementation "org.jetbrains.kotlin:kotlin-reflect:1.7.21"

                implementation "ch.qos.logback:logback-classic:1.4.5"
            }
        }
        jvmTest {

        }
        jsMain {
            dependencies {
                implementation npm("dygraphs", "2.1.0")
            }
            compileKotlinJs {
                kotlinOptions.sourceMap = false
                //kotlinOptions.moduleKind = "umd"
            }
        }
        jsTest {

        }
    }
}

application {
    mainClassName = 'StoveServerKt'
}

tasks.named('jvmProcessResources') {
    def jsBrowserDistribution = tasks.named('jsBrowserDistribution')
    from(jsBrowserDistribution)
}

tasks.named('run') {
    dependsOn(tasks.named('jvmJar'))
    classpath(tasks.named('jvmJar'))
}

shadowJar {
    manifest {
        attributes 'Main-Class': 'StoveServerKt'
    }
}

task deployOnPi {
    dependsOn(clean) // this may be stupid, but kotlin serialization compiler plugin has an issue, on the second build without clean, it complains about serizalizers not found.
    dependsOn(shadowJar)
    doLast {
        exec {
            commandLine('scp', "build/libs/pistove-${version}-all.jar", System.properties['pistove.raspberry.pi.scp'])
        }
    }
}
